#!/usr/bin/env zsh

set -eo pipefail

# HELP: delete all traces of the setup created by start_pairing

function user_exists() {
  local pair_user="$1"
  local pair_user_dir="$2"

  if is_mac; then
    dscl . list "$pair_user_dir"  > /dev/null 2>&1
  else
    id -u "$pair_user"
  fi
}

function delete_stuff() {
  local pair_user="$1"
  local pair_user_dir="$2"

  if is_mac; then
    while get_a_group_the_user_is_in_and_remove_user_from_group "$pair_user"; do
      echo > /dev/null
    done
  fi

  if user_exists "$pair_user" "$pair_user_dir" ; then

    # remove all processes for pair user to ensure we can fully cleanup
    pids=$(ps -u "$pair_user" -o 'pid=' || echo '')
    if [[ -n "$pids" ]]; then
      while read -r pid ; do
        set +e
        sudo kill $pid
        set -e
      done<<<"$pids"
    fi

    echo "deleting $pair_user user"
    if is_mac; then
      sudo dscl . -delete "$pair_user_dir"
    else
      sudo userdel --remove "$pair_user"
    fi
  fi
  if [[ -d "$pair_user_dir" ]]; then
    echo "deleting $pair_user user home directory"
    sudo rm -rf "$pair_user_dir"
  fi
}

function wemux_kick_all() {
  local pair_user="$1"
  if grep -q "$pair_user" <<<"$(wemux users)"; then
    wemux kick "$pair_user"
  fi
}

### MAC SPECIFIC FUNCTIONS
function remote_user_from_group() {
  local pair_user="$1"
  local group="$2"
  if [[ -n "$group" ]]; then
    echo "deleting $pair_user from group $group"
    sudo dscl . -delete /Groups/$group GroupMembership "$pair_user"
  fi
}

function get_a_group_the_user_is_in_and_remove_user_from_group() {
  local pair_user="$1"
  group="$(random_group_the_user_is_in "$pair_user")"
  if [[ -n "$group" ]]; then
    remote_user_from_group "$pair_user" "$group"
    return 0
  else
    return 1
  fi
}

function groups_for_user() {
  local pair_user="$1"
  dscl . search /Groups GroupMembership "$pair_user" | grep = | awk '{print $1}'
}

function random_group_the_user_is_in() {
  local pair_user="$1"
  groups_for_user "$pair_user" | head -n 1
}
###############################

if [[ -f /var/tmp/no_term_limits_wemux_ssh_users ]] ; then
  while read -r pair_user ; do
    if [[ "$pair_user" != "$(whoami)" ]]; then
      if is_mac; then
        pair_user_dir="/Users/${pair_user}"
      else
        pair_user_dir="/home/${pair_user}"
      fi

      if grep -q "$(whoami)" <<<"$pair_user" || grep -q "$(whoami)" <<<"$pair_user_dir"; then
        >&2 echo "Given pair user - '${pair_user}' - and / or pair user home directory - '${pair_user_dir}' - contains current user '$(whoami)'. Will NOT delete."
        exit 1
      fi

      wemux_kick_all "$pair_user"
      delete_stuff "$pair_user" "$pair_user_dir"
    fi
  done </var/tmp/no_term_limits_wemux_ssh_users
fi
